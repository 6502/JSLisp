(import * from gui)

(defun main ()
  (with-window (w (100 100 500 320 :title "Test window" :close false)
                  ((name (input "name"))
                   (address (input "address"))
                   (email (input "e-mail"))
                   (phone (input "phone"))
                   (age (select "age" (append (range 1 100) "100+")))
                   (newsletter (checkbox "YES! I want to subscribe to the free newsletter"))
                   (spam (checkbox "YES! I want to feed my mailbox with unrelated spam"))
                   (freebies (group "Freebies"))
                   (ok (button "OK" (lambda ()
                                      (display ~"name: {(text name)}")
                                      (display ~"address: {(text address)}")
                                      (display ~"email: {(text email)}")
                                      (display ~"phone: {(text phone)}")
                                      (display ~"age: {(selection age)}")
                                      (display ~"newletter: {(checked newsletter)}")
                                      (display ~"spam: {(checked spam)}")
                                      (hide-window w))))
                   (cancel (button "Cancel" (lambda () (hide-window w)))))
                  (:V :spacing 8 :border 8
                      (:H :size 35 (:Hdiv name))
                      (:H :size 35 (:Hdiv address))
                      (:H :size 35 (:H (:Hdiv email) (:Hdiv phone)) (:Hdiv age :size 70))
                      (:V :size 10)
                      (:V :size 75
                          (:Vdiv freebies :size 75 :border 8 :spacing 4
                                 (:V :size 3)
                                 (:H :size 20 (:Hdiv newsletter))
                                 (:H :size 20 (:Hdiv spam))))
                      (:H)
                      (:H :size 30
                          (:H)
                          (:Hdiv ok :size 80)
                          (:Hdiv cancel :size 80)
                          (:H))))
    (show-window w)
    (setf (selection age) "45")
    (setf (text name) "Andrea Griffini")
    (setf (text address) "Via somewhere, somecivic")
    (setf (text email) "agriffini@jslisp.org")
    (setf (text phone) "555 123 4567")
    (setf (checked newsletter) true)
    (setf (checked spam) true)))

(main)
