(import * from gui)
(defconstant SHIFT 7)
(defconstant SZ (ash 1 SHIFT))
(defconstant MASK (1- (* SZ SZ)))
(defconstant SF 2)
(import * from layout)
(import * from graphics)

(defun life ()
  (let** ((w (window 0 0 (+ (* SZ SF) 40) (+ (* SZ SF) 100)
                     title: "The Game of Life"))
          (field (make-array (* SZ SZ) 0))
          (display (add-widget w (set-style (create-element "canvas")
                                            position "absolute"
                                            px/width (* SF SZ)
                                            px/height (* SF SZ))))
          (run/stop (add-widget w (button "Run" #'run/stop)))
          (step (add-widget w (button "Step" #'step)))
          (test (add-widget w (button "Test" #'test)))
          (random (add-widget w (button "Random" #'random)))
          (run-timer null)
          (alive (list))
          (#'recalc ()
            (with-canvas display
              (let ((new-field (make-array (* SZ SZ) 0))
                    (checklist (list))
                    (new-alive (list)))
                (macrolet ((cell (i) `(aref field (logand ,i MASK)))
                           (new-cell (i) `(aref new-field (logand ,i MASK)))
                           (nhbor (fn var item)
                                  `(,fn (,item (- ,var (1+ SZ)))
                                        (,item (- ,var SZ))
                                        (,item (- ,var (1- SZ)))
                                        (,item (- ,var 1))
                                        (,item (+ ,var 1))
                                        (,item (+ ,var (1- SZ)))
                                        (,item (+ ,var SZ))
                                        (,item (+ ,var (1+ SZ))))))
                  (labels ((check (i)
                             (setf i (logand i MASK))
                             (unless (aref new-field i)
                               (setf (aref new-field i) 1)
                               (push i checklist))))
                    (dolist (x alive)
                      (check x)
                      (nhbor progn x check))
                    (dolist (i checklist)
                      (let ((n (nhbor + i cell)))
                        (if (or (= n 3)
                                (and (= n 2) (cell i)))
                            (progn
                              (unless (cell i)
                                (fill-style "#000000")
                                (fill-rect (* SF (logand i (1- SZ)))
                                           (* SF (ash i (- SHIFT)))
                                           SF SF))
                              (setf (new-cell i) 1)
                              (push i new-alive))
                            (progn
                              (when (cell i)
                                (fill-style "#EEEEEE")
                                (fill-rect (* SF (logand i (1- SZ)))
                                           (* SF (ash i (- SHIFT)))
                                           SF SF))
                              (setf (new-cell i) 0)))))))
                (setf field new-field)
                (setf alive new-alive))))
          (#'refresh ()
            (setf display.width (* SF SZ))
            (setf display.height (* SF SZ))
            (with-canvas display
              (let ((p 0))
                (dotimes (y SZ)
                  (dotimes (x SZ)
                    (fill-style (if (aref field p) "#000000" "#EEEEEE"))
                    (fill-rect (* SF x) (* SF y) SF SF)
                    (incf p))))))
          (#'run/stop ()
            (if (null? run-timer)
                (progn
                  (setf run-timer (set-interval #'recalc 10))
                  (setf run/stop.value "Stop"))
                (progn
                  (clear-interval run-timer)
                  (setf run-timer null)
                  (setf run/stop.value "Run"))))
          (#'step ()
              (recalc))
          (#'test ()
            (setf alive (list))
            (dotimes (i (* SZ SZ))
              (when (setf (aref field i)
                          (let* ((img '("::::::::::::::::::::::::#:::::::::::"
                                        "::::::::::::::::::::::#:#:::::::::::"
                                        "::::::::::::##::::::##::::::::::::##"
                                        ":::::::::::#:::#::::##::::::::::::##"
                                        "##::::::::#:::::#:::##::::::::::::::"
                                        "##::::::::#:::#:##::::#:#:::::::::::"
                                        "::::::::::#:::::#:::::::#:::::::::::"
                                        ":::::::::::#:::#::::::::::::::::::::"
                                        "::::::::::::##::::::::::::::::::::::"))
                                 (y (+ (ash i (- SHIFT))
                                       (- (ash SZ -1))
                                       (ash (length img) -1)))
                                 (x (+ (logand i (1- SZ))
                                       (- (ash SZ -1))
                                       (ash (length (aref img 0)) -1))))
                            (if (and (< -1 y (length img))
                                     (< -1 x (length (aref img 0)))
                                     (= (aref img y x) "#"))
                                1 0)))
                (push i alive)))
            (refresh))
          (#'random ()
            (setf alive (list))
            (dotimes (i (* SZ SZ))
              (when (setf (aref field i)
                          (random-int 2))
                (push i alive)))
            (refresh)))
    (set-layout w (V spacing: 8 border: 8
                     :filler:
                     size: (* SF SZ)
                     (H :filler: size: (* SF SZ) (dom display) :filler:)
                     :filler:
                     size: 30
                     (H :filler:
                        size: 60
                        (dom run/stop)
                        (dom step)
                        (dom test)
                        (dom random)
                        :filler:)))
    (setf w.close-cback
          (lambda () (when run-timer (run/stop))))
    (random)
    (show-window w center: true)))

(defun main ()
  (life))

(main)
